// Vickrey Auction: Winner pays the second-highest bid, losers get deposits back.
// Distribution semantics: pot (400 wei) - winner pays second price to Seller, all get deposits back

type bid = {0 .. 2}

macro max2(a: bid, b: bid): bid = (a >= b ? a : b);
macro max3(a: bid, b: bid, c: bid): bid = max2(max2(a, b), c);
macro secondMax(a: bid, b: bid, c: bid): bid =
    (a == max3(a, b, c)) ? max2(b, c)
  : (b == max3(a, b, c)) ? max2(a, c)
  : max2(a, b);

macro isWinner(myBid: bid, b1: bid, b2: bid, b3: bid): bool = (myBid == max3(b1, b2, b3));

game main() {
  join Seller() $ 100 B1() $ 100 B2() $ 100 B3() $ 100;

  // All bids are hidden and submitted independently
  // If any bidder quits, survivors split the forfeited deposit
  yield or split
    B1(b: bid)
    B2(b: bid)
    B3(b: bid);

  withdraw {
      Seller -> 100 + secondMax(B1.b, B2.b, B3.b);
      B1 -> isWinner(B1.b, B1.b, B2.b, B3.b) ? (100 - secondMax(B1.b, B2.b, B3.b)) : 100;
      B2 -> isWinner(B2.b, B1.b, B2.b, B3.b) ? (100 - secondMax(B1.b, B2.b, B3.b)) : 100;
      B3 -> isWinner(B3.b, B1.b, B2.b, B3.b) ? (100 - secondMax(B1.b, B2.b, B3.b)) : 100;
  }
}
