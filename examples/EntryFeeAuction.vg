// Entry-fee auction: bidders decide to enter and bid; highest bid wins, entry fee is charged if entered.
// Distribution semantics: pot (300 wei) - entry fee 10, winning bid*8 goes to Seller

type bid = {0 .. 10}
macro winningBid(e1: bool, b1: bid, e2: bool, b2: bid): bid =
    (!e1 && !e2) ? 0
  : (e1 && !e2) ? b1
  : (!e1 && e2) ? b2
  : (b1 >= b2 ? b1 : b2);

game main() {
  join Seller() $ 100 B1() $ 100 B2() $ 100;
  
  yield B1(enter: bool) B2(enter: bool);
  yield B1(b: bid) where B1.enter || { Seller -> 150; B2 -> 150 };
  yield B2(b: bid) where B2.enter || { Seller -> 150; B1 -> 150 };

  withdraw (B1.enter == null && B2.enter == null) ?
      { Seller -> 100; B1 -> 100; B2 -> 100; }  // Both quit at entry
    : (B1.enter == null) ?
      { Seller -> 125; B1 -> 50; B2 -> 125; }  // B1 quit at entry
    : (B2.enter == null) ?
      { Seller -> 125; B1 -> 125; B2 -> 50; }  // B2 quit at entry
    : {
      Seller -> 100
          + (B1.enter ? 10 : 0)
          + (B2.enter ? 10 : 0)
          + winningBid(B1.enter, B1.b, B2.enter, B2.b) * 8;

      B1 ->
          !B1.enter ? 100
          : ((!B2.enter || B1.b >= B2.b) ? (100 - 10 - B1.b * 8) : (100 - 10));

      B2 ->
          !B2.enter ? 100
          : ((!B1.enter || B2.b > B1.b) ? (100 - 10 - B2.b * 8) : (100 - 10));
    }
}
